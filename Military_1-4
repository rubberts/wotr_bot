===phase_1_military===
~ corruption_strategy = false
<b>Phase 1</b> # CLASS: w3-green
Recover action dice # CLASS: w3-orange
-> phase_1_military_2

===phase_1_military_2===
Draw Event <> 
{ warriors_of_middle_earth:
    and Faction Event <>
}
cards # CLASS: w3-orange
+ [Done] -> phase_1_military_4

===phase_1_military_4===
Shadow victory points lower than Corruption points? # CLASS: w3-yellow
+ [Yes] -> phase_1_from_military_strategy # IMAGE: corruption_strategy.jpg
+ [No] -> phase_1_military_3

===phase_1_military_3===
More than 6 Event cards in hand? # CLASS: w3-yellow
+ [Yes] -> phase_1_military_discard
+ [No]
{ warriors_of_middle_earth:
 -> wome_phase_1_military_faction_event 
 - else:
 -> phase_3_military
}

===phase_1_military_discard===
Event card discard priority: # CLASS: w3-orange
1. Doesn't use the term "Fellowship Revealed" # CLASS: w3-orange
2. Character card # CLASS: w3-orange
3. Strategy card # CLASS: w3-orange
4. Descending order of priority # CLASS: w3-orange
5. Doesn't place a tile # CLASS: w3-orange
+ [Done]
{ warriors_of_middle_earth:
 -> wome_phase_1_military_faction_event
 - else:
 -> phase_3_military
}

===wome_phase_1_military_faction_event===
More than 4 Faction Event cards in hand? # CLASS: w3-yellow
+ [Yes] -> wome_military_discard_priority
+ [No] -> phase_3_military

===wome_military_discard_priority===
priority of Faction Event cards to discard: # CLASS: w3-orange
1. No faction picture # CLASS: w3-orange
2. Faction not in play # CLASS: w3-orange
3. Not a preferred card # CLASS: w3-orange
4. Doesn't use the term "Fellowship revealed" # CLASS: w3-orange
5. Doesn't place a tile # CLASS: w3-orange
+ [Done] -> phase_3_military

===phase_3_military===
<b>Phase 3</b> # CLASS: w3-green
Select first Fellowship state, starting from top: # CLASS: w3-yellow
+ Fellowship on the Mordor track # CLASS: w3-yellow
 -> phase_3_military_max
+ Fellowship Progress counter is greater than 5 # CLASS: w3-yellow
 -> phase_3_military_2
+ Fellowship in starting location and Progress is 0 # CLASS: w3-yellow
 -> phase_3_military_0
+ None of the above # CLASS: w3-yellow
 -> phase_3_military_1

===phase_3_military_max===
Assign maximum dice allowed to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_military

===phase_3_military_0===
Assign 0 dice to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_military

===phase_3_military_1===
Assign 1 die to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_military

===phase_3_military_2===
Assign 2 dice to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_military

===phase_4_military===
<b>Phase 4</b> # CLASS: w3-green
Roll remaining action dice # CLASS: w3-orange
+ [Done] -> phase_5_military