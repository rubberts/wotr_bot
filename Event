===event===
<b>Event</b> # CLASS: w3-green
Holding a card with a playable <> # CLASS: w3-yellow
{ 
- corruption_strategy:
    Character symbol? # CLASS: w3-yellow
- else:
    Army or Muster symbol? # CLASS: w3-yellow
}
+ [Yes] -> event_priority_1
+ [No] -> event_using_event_die

===event_using_event_die===
Using an Event die? # CLASS: w3-yellow  # IMAGE: ADSAevent.png
+ [Yes] -> event_holding_event_cards
+ [No] -> return_knot

===event_holding_event_cards===
Holding less than 4 Event cards<>
{ warriors_of_middle_earth:
    , excluding Faction Event cards<>
}
? # CLASS: w3-blue
+ [Yes] -> draw_preferred_event_card
 + [No]
{ warriors_of_middle_earth:
 -> wome_holding_less_3
 - else:
 -> event_holding_playable_event
}

===wome_holding_less_3===
Holding less than 3 Faction Event cards? # CLASS: w3-blue
+ [Yes] -> warriors_draw_faction_event_card
+ [No] -> event_holding_playable_event

===event_holding_playable_event===
Holding a playable Event <>
{ warriors_of_middle_earth:
    or Faction Event <>
}
card? # CLASS: w3-blue
+ [Yes] -> event_priority_1
+ [No] -> event_draw_event_card

===event_draw_event_card===
Draw a card with <>
{ 
- corruption_strategy:
    a Character symbol. # CLASS: w3-yellow
- else:
    an Army or Muster symbol. # CLASS: w3-yellow
}
Above full_hand_tooltip? # CLASS: w3-blue
+ [Yes] -> event_priority_3
+ [No] -> end_action

===event_priority_1===
<b>Event <>
{ warriors_of_middle_earth:
    or Faction Event <>
}
card play priority:</b> # CLASS: w3-purple
1. Full_hand_tooltip # CLASS: w3-purple
2. <>
{ 
- corruption_strategy:
    Character symbol <>
- else:
    Army or Muster symbol <>
}
Event card # CLASS: w3-purple
{ warriors_of_middle_earth:
3. <>
{ 
- corruption_strategy:
    Character symbol <>
- else:
    Army or Muster symbol <>
}
Faction Event card # CLASS: w3-purple
4. Ascending order of initiative # CLASS: w3-purple
- else:
3. Ascending order of initiative # CLASS: w3-purple
}
+ [Done] -> play_event_faction_card

===event_priority_2===
<b>Event <>
{ warriors_of_middle_earth:
    or Faction Event <>
}
card play priority:</b> # CLASS: w3-purple
1. Full_hand_tooltip # CLASS: w3-purple
2. Event card # CLASS: w3-purple
{ warriors_of_middle_earth:
3. Faction Event card # CLASS: w3-purple
4. Ascending order of initiative # CLASS: w3-purple
- else:
3. Ascending order of initiative # CLASS: w3-purple
}
+ [Done] -> play_event_faction_card

===event_priority_3===
<b>Discard card priority:</b> # CLASS: w3-purple
{ 
- corruption_strategy:
    1. Not a character symbol card # CLASS: w3-purple
- else:
    1. Not an army or muster symbol card # CLASS: w3-purple
}
2. Doesn't use the term "Fellowship revealed" # CLASS: w3-purple
3. Doesn't place a tile # CLASS: w3-purple
4. Descending order of initiative # CLASS: w3-purple
+ [Done] -> discard

===event_card_discard===
<b>Event card discard</b> -> event_priority_3 # CLASS: w3-green

===event_2===
<b>Event 2</b>  # CLASS: w3-green
Holding an "if Fellowship revealed" card? # CLASS: w3-yellow
+ [Yes] -> play_event_faction_card
+ [No] -> event_2_corruption

===event_2_corruption===
Holding a card which can add corruption to the Fellowship or adds a Hunt tile? # CLASS: w3-yellow
+ [Yes] -> play_event_faction_card
+ [No] -> event_2_few_cards

===event_2_few_cards===
Holding less than 4 Event cards<>
{ warriors_of_middle_earth:
    , excluding Faction Event cards<>
}
? # CLASS: w3-yellow
+ [Yes] -> draw_character_event_card
+ [No] -> return_knot
