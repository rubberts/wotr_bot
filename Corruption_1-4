===phase_1_corruption===
~ corruption_strategy = true
<b>Phase 1</b> # CLASS: w3-green
Recover action dice # CLASS: w3-orange
-> phase_1_corruption_2

===phase_1_corruption_2===
Draw Event <> 
{ warriors_of_middle_earth:
    and Faction Event <>
}
cards # CLASS: w3-orange
+ [Done] -> phase_1_corruption_3

===phase_1_corruption_3===
More than 6 Event cards in hand? # CLASS: w3-yellow
+ [Yes] -> phase_1_corruption_discard
+ [No]
{ warriors_of_middle_earth:
 -> wome_phase_1_corruption_faction_event 
 - else:
 -> phase_2_corruption
}

===phase_1_corruption_discard===
Holding more than 1 strategy card? # CLASS: w3-blue
+ [Yes] -> phase_1_corruption_discard_strategy
+ [No] -> phase_1_corruption_discard_character

===phase_1_corruption_discard_strategy===
Event card discard priority: # CLASS: w3-orange
1. Doesn't use the term "Fellowship Revealed" # CLASS: w3-orange
2. Doesn't place a tile # CLASS: w3-orange
3. Strategy card # CLASS: w3-orange
4. Character card # CLASS: w3-orange
5. Descending order of priority # CLASS: w3-orange
+ [Done]
{ warriors_of_middle_earth:
 -> wome_phase_1_corruption_faction_event 
 - else:
 -> phase_2_corruption
}

===phase_1_corruption_discard_character===
Event card discard priority: # CLASS: w3-orange
1. Doesn't use the term "Fellowship Revealed" # CLASS: w3-orange
2. Doesn't place a tile # CLASS: w3-orange
3. Character card # CLASS: w3-orange
4. Strategy card # CLASS: w3-orange
5. Descending order of priority # CLASS: w3-orange
+ [Done] -> phase_2_corruption

===wome_phase_1_corruption_faction_event===
More than 4 Faction Event cards in hand? # CLASS: w3-yellow
+ [Yes] -> wome_corruption_discard_priority
+ [No] -> phase_2_corruption

===wome_corruption_discard_priority===
Priority of Faction Event cards to discard: # CLASS: w3-orange
1. No faction picture # CLASS: w3-orange
2. Faction not in play # CLASS: w3-orange
3. Not a preferred card # CLASS: w3-orange
4. Doesn't use the term "Fellowship revealed" # CLASS: w3-orange
5. Doesn't place a tile # CLASS: w3-orange
+ [Done] -> phase_2_corruption

===phase_2_corruption===
<b>Phase 2</b> # CLASS: w3-green
Corruption points lower than Shadow Victory points? # CLASS: w3-yellow
+ [Yes] -> phase_3_military # IMAGE: military_strategy.jpg

+ [No] -> phase_3_corruption

===phase_3_corruption===
<b>Phase 3</b> # CLASS: w3-green
Select first Fellowship state, starting from top: # CLASS: w3-yellow
+ Fellowship at starting location and progress is 0 # CLASS: w3-yellow
 -> phase_3_corruption_random
+ Fellowship on the Mordor track # CLASS: w3-yellow
 -> phase_3_corruption_max
+ Mobile_tooltip army adjacent to target_tooltip which would win the game or Shadow only has 7 dice # CLASS: w3-yellow
 -> phase_3_corruption_1
+ Fellowship progress is greater than 4 # CLASS: w3-yellow
 -> phase_3_corruption_2
+ Shortest Fellowship route leads via a Shadow stronghold and they are within two progress of it or have passed it # CLASS: w3-yellow
 -> phase_3_corruption_2
+ None of the above # CLASS: w3-yellow
 -> phase_3_corruption_1

===phase_3_corruption_random===
{ RANDOM(1, 2):
- 1: -> phase_3_corruption_0
- 2: -> phase_3_corruption_1
}

===phase_3_corruption_max===
Assign maximum dice allowed to the hunt pool # CLASS: w3-orange  # IMAGE: hunt_pool.png
+ [Done] -> phase_4_corruption

===phase_3_corruption_0===
Assign 0 dice to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_corruption

===phase_3_corruption_1===
Assign 1 die to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_corruption

===phase_3_corruption_2===
Assign 2 dice to the hunt pool # CLASS: w3-orange # IMAGE: hunt_pool.png
+ [Done] -> phase_4_corruption

===phase_4_corruption===
<b>Phase 4</b> # CLASS: w3-green
Roll remaining action dice # CLASS: w3-orange
+ [Done] -> phase_5_corruption